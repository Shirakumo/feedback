(in-package #:feedback)

(defun output (object message url-format &rest args)
  (let ((target (uri-to-url (apply #'format NIL url-format args)
                            :representation :external :query `(("message" . ,message)))))
    (if (string= "true" (post/get "browser"))
        (redirect target)
        (api-output object :message message :target target))))

(define-api feedback/project/list () (:access (perm feedback project list))
  (api-output (list-projects)))

(define-api feedback/project/new (name &optional trace-data-type description attachment-name[] attachment-type[]) (:access (perm feedback project new))
  (output (make-project name :description description
                             :trace-data-type trace-data-type
                             :attachments (loop for name in attachment-name[]
                                                for type in attachment-type[]
                                                collect (list name type)))
          "Project created." "feedback/~a/" name))

(define-api feedback/project/edit (project &optional name trace-data-type description attachment-name[] attachment-type[] member[]) (:access (perm feedback project edit))
  (output (edit-project project :name name
                                :description description
                                :trace-data-type trace-data-type
                                :attachments (loop for name in attachment-name[]
                                                   for type in attachment-type[]
                                                   collect (list name type))
                                :members member[])
          "Project edited." "feedback/~a/" name))

(define-api feedback/project/delete (project) (:access (perm feedback project delete))
  (delete-project (ensure-project project))
  (output NIL "Project deleted." "feedback/"))

(define-api feedback/entry/list (project) (:access (perm feedback entry list))
  (api-output (list-entries (ensure-project project))))

(defun feedback-mail (project url description)
  (format NIL "Hello,

New feedback has been submitted for the ~a project.
You can view the feedback report here:

  ~a
~@[~%The provided description was:~%~%  ~a~%~]
----
This is an automated message generated by Feedback.
Please do not respond to this."
          project url description))

(define-api feedback/entry/new (project user-id &optional os-type cpu-type gpu-type version os-info cpu-info gpu-info description assigned-to severity relates-to) (:access (perm feedback entry new))
  (db:with-transaction ()
    (let* ((project (or (find-project project)
                        (ensure-project project)))
           (project-name (dm:field project "name"))
           (types (list-attachments project))
           (entry (make-entry project :version version
                                      :user-id user-id
                                      :os-type os-type
                                      :os-info os-info
                                      :cpu-type cpu-type
                                      :cpu-info cpu-info
                                      :gpu-type gpu-type
                                      :gpu-info gpu-info
                                      :description description
                                      :assigned-to assigned-to
                                      :relates-to relates-to
                                      :severity severity))
           (url (format NIL "feedback/~a/entry/~a" project-name (dm:id entry))))
      (loop for type in types
            for file = (post-var (dm:field type "name"))
            do (when file
                 (ensure-directories-exist (attachment-pathname entry type))
                 (uiop:copy-file (first file) (attachment-pathname entry type))))
      (when (config :recipient)
        (mail:send (config :recipient)
                   (format NIL "New feedback for ~a" project-name)
                   (feedback-mail project-name (uri-to-url url :representation :external) description)))
      (output entry "Feedback submitted." url))))

(define-api feedback/entry/edit (entry &optional comment description status assigned-to severity relates-to) (:access (perm feedback entry edit))
  (db:with-transaction ()
    (let* ((entry (ensure-entry entry))
           (project (ensure-project entry)))
      (edit-entry entry :description description :comment comment :status status
                        :assigned-to assigned-to :severity severity :relates-to relates-to)
      (output entry "Entry updated." "feedback/~a/entry/~a" (dm:field project "name") (dm:id entry)))))

(define-api feedback/entry/delete (entry) (:access (perm feedback entry delete))
  (let ((entry (ensure-entry entry)))
    (delete-entry entry)
    (output NIL "Entry deleted." "feedback/~a/" (dm:field (ensure-project entry) "name"))))

(define-api feedback/note/new (entry text) (:access (perm feedback note new))
  (db:with-transaction ()
    (let* ((entry (ensure-entry entry))
           (project (ensure-project (dm:field entry "project")))
           (note (make-note entry text)))
      (output entry "Note created." "feedback/~a/entry/~a" (dm:field project "name") (dm:id entry)))))

(define-api feedback/note/edit (note text) (:access (perm feedback note edit))
  (db:with-transaction ()
    (let ((note (edit-note note :text text)))
      (output entry "Note edited." "feedback/~a/entry/~a" (dm:field (ensure-project note) "name") (dm:id entry)))))

(define-api feedback/note/delete (note) (:access (perm feedback note delete))
  (let ((entry (ensure-note note)))
    (delete-note note)
    (output entry "Note edited." "feedback/~a/entry/~a" (dm:field (ensure-project note) "name") (dm:id entry))))

(define-api feedback/snapshot/new (project user-id session-id session-duration snapshot-duration &optional version trace) (:access (perm feedback entry new))
  (db:with-transaction ()
    (let* ((project (or (find-project project)
                        (ensure-project project)))
           (snapshot (make-snapshot project :version version
                                            :user-id user-id
                                            :session-id session-id
                                            :session-duration (parse-integer session-duration)
                                            :snapshot-duration (parse-integer snapshot-duration)
                                            :version version)))
      (when trace
        (let ((path (make-pathname :name "trace" :type "dat" :defaults (snapshot-directory snapshot))))
          (ensure-directories-exist path)
          (uiop:copy-file (first trace) path)))
      (output snapshot "Snapshot submitted." "feedback/~a/snapshot/~a" (dm:field project "name") (dm:id snapshot)))))

(define-api feedback/snapshot/delete (snapshot) (:access (perm feedback entry delete))
  (let ((snapshot (ensure-snapshot snapshot)))
    (delete-snapshot snapshot)
    (output NIL "Snapshot deleted." "feedback/~a/" (dm:field (ensure-project snapshot) "name"))))
